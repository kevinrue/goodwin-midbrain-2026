import os
import pandas as pd

# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"

# read sample sheet
samples = (
    pd.read_csv(config["samplesheet"], sep="\t", dtype={"sample": str})
    .set_index("id", drop=False)
    .sort_index()
)

# target rules
# -----------------------------------------------------
rule all:
    input:
        expand("results/dropletqc/{id}.tsv.gz", id=samples.index)

rule install:
    output:
        path=directory("results/library"),
    log: "logs/install.log",
    container: "docker://bioconductor/bioconductor_docker:RELEASE_3_20"
    threads: 4
    resources:
        mem="8G",
        runtime="30m",
    script:
        "scripts/install.R"

rule dropletqc:
    input:
        bam=lookup(query="id == '{id}'", within=samples, cols="bam"),
        barcodes=lookup(query="id == '{id}'", within=samples, cols="barcodes"),
        rlib="results/library",
    params:
        id="{id}",
    output:
        tsv="results/dropletqc/{id}.tsv.gz",
    log: "logs/dropletqc/{id}.log",
    container: "docker://bioconductor/bioconductor_docker:RELEASE_3_20"
    threads: 16
    resources:
        mem="64G",
        runtime="30m",
    script:
        "scripts/dropletqc.R"
