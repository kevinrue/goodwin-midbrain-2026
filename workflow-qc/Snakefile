import os
import pandas as pd

# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"

# read sample sheet
samples = (
    pd.read_csv(config["samplesheet"], sep="\t", dtype={"sample": str})
    .set_index("id", drop=False)
    .sort_index()
)

# target rules
# -----------------------------------------------------
rule all:
    input:
        "reports/index.html",
        "reports/umi_stats_all.html",
        expand("results/barcodeRanks/{sample}.yaml", sample=samples.index)

rule umi_stats:
    input:
        raw="data/{sample}/outs/multi/count/raw_feature_bc_matrix.h5",
        filtered="data/{sample}/outs/per_sample_outs/{sample}/count/sample_filtered_feature_bc_matrix/barcodes.tsv.gz",
        gtf=config["gtf"],
    output:
        tsv="results/umi_stats/{sample}.tsv.gz",
    conda:
        "envs/umi_stats.yaml",
    log:
        "logs/umi_stats/{sample}.log",
    threads: 1
    resources:
        mem_mb=8 * 1024,
        runtime="30m",
    script:
        "scripts/umi_stats.R"

rule umi_stats_one:
    input:
        umi_stats="results/umi_stats/{sample}.tsv.gz",
    output:
        html="reports/umi_stats_one/{sample}.html",
    params:
        title=config["experiment"]["title"],
    conda:
        "envs/umi_stats_one.yaml"
    threads: 1
    resources:
        mem="8G",
        runtime="10m",
    script:
        "notebooks/umi_stats_one.Rmd"

rule umi_stats_all:
    input:
        umi_stats=expand("results/umi_stats/{sample}.tsv.gz", sample=samples.index),
    output:
        html="reports/umi_stats_all.html",
    params:
        title=config["experiment"]["title"],
        umi_min=config["filter"]["barcodes"]["umi_min"],
        genes_min=config["filter"]["barcodes"]["genes_min"],
    conda:
        "envs/umi_stats_one.yaml"
    threads: 1
    resources:
        mem="16G",
        runtime="20m",
    script:
        "notebooks/umi_stats_all.Rmd"

rule barcodeRanks:
    input:
        raw="data/{sample}/outs/multi/count/raw_feature_bc_matrix.h5",
        umi_stats="results/umi_stats/{sample}.tsv.gz",
    output:
        yaml="results/barcodeRanks/{sample}.yaml",
    conda:
        "envs/barcodeRanks.yaml",
    log:
        "logs/barcodeRanks/{sample}.log",
    threads: 8
    resources:
        mem_mb="8G",
        runtime="30m",
    script:
        "scripts/barcodeRanks.R"


rule report_index:
    input:
        expand("reports/umi_stats_one/{sample}.html", sample=samples.index),
        "reports/umi_stats_all.html",
    params:
        samples=config["samplesheet"],
        title=config["experiment"]["title"],
    output:
        "reports/index.html",
    conda:
        "envs/umi_stats_one.yaml"
    threads: 1
    resources:
        mem="8G",
        runtime="10m",
    script:
        "notebooks/index.Rmd"
