import os
import pandas as pd

# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"

# read sample sheet
samples = (
    pd.read_csv(config["samplesheet"], sep="\t", dtype={"pool": str})
    .set_index("pool", drop=False)
    .sort_index()
)

rule all:
    input:
        expand("results/vireo/{pool}", pool=samples["pool"].tolist()),
        "reports/report.html",
        # expand("results/vireo_de_novo/{pool}", pool=samples["pool"].tolist()),
        # expand("results/vireo_force_gt/{pool}", pool=samples["pool"].tolist()),

rule filter_vcf_samples:
    input:
        config["vcf"]["path"]
    output:
        "results/vcf/samples.vcf.gz",
    log:
        "logs/filter_vcf_samples.log",
    params:
        extra="--samples {} --include 'FMT/GT=\"het\"'".format(config["vcf"]["samples"])
    threads: 8
    resources:
        mem="8G",
        runtime="5m",
    wrapper:
        "v9.0.0/bio/bcftools/view"

rule cellsnp_lite:
    input:
        bam=lookup(query="pool == '{pool}'", within=samples, cols="bam"),
        barcodes=lookup(query="pool == '{pool}'", within=samples, cols="barcodes"),
        vcf="results/vcf/samples.vcf.gz",
    output:
        dir=directory("results/cellsnp_lite/{pool}"),
    params:
        max_depth=lookup(within=config, dpath="cellsnp_lite/max_depth")
    log:
        "logs/cellsnp_lite/{pool}.log",
    conda:
        "envs/vireo.yml",
    threads: 16
    resources:
        mem="64G",
        runtime="4h",
    shell:
        "cellsnp-lite "
        " -s {input.bam} "
        " -b {input.barcodes} "
        " -R {input.vcf} "
        " -O {output.dir} "
        " --maxDEPTH {params.max_depth}"
        " -p {threads} "
        " --minMAF 0.1 "
        " --minCOUNT 20 "
        " --gzip "
        " 2> {log} "

rule vireo:
    input:
        cellsnp="results/cellsnp_lite/{pool}",
        vcf="results/vcf/samples.vcf.gz",
    output:
        dir=directory("results/vireo/{pool}"),
    log:
        "results/vireo/{pool}.log",
    conda:
        "envs/vireo.yml",
    threads: 16
    resources:
        mem="16G",
        runtime="1h",
    shell:
        "vireo "
        " -c {input.cellsnp}"
        " -d {input.vcf}"
        " -o {output.dir}"
        " 2> {log} "

rule report:
    input:
        vireo_dir=expand("results/vireo/{pool}", pool=samples["pool"].tolist()),
    output:
        html="reports/report.html",
    conda:
        "envs/umi_stats_one.yaml"
    threads: 1
    resources:
        mem="8G",
        runtime="20m",
    script:
        "notebooks/report.Rmd"

rule vireo_de_novo:
    input:
        cellsnp="results/cellsnp_lite/{pool}",
    output:
        dir=directory("results/vireo_de_novo/{pool}"),
    log:
        "results/vireo_de_novo/{pool}.log",
    conda:
        "envs/vireo.yml",
    threads: 16
    resources:
        mem="32G",
        runtime="12h",
    shell:
        "vireo "
        " -c {input.cellsnp}"
        " -N 16" # TODO configure
        " -o {output.dir}"
        " 2> {log} "

rule vireo_force_gt:
    input:
        cellsnp="results/cellsnp_lite/{pool}",
        vcf="results/vcf/samples.vcf.gz",
    output:
        dir=directory("results/vireo_force_gt/{pool}"),
    log:
        "results/vireo_force_gt/{pool}.log",
    conda:
        "envs/vireo.yml",
    threads: 16
    resources:
        mem="32G",
        runtime="12h",
    shell:
        "vireo "
        " -c {input.cellsnp}"
        " -d {input.vcf}"
        " -o {output.dir}"
        " --forceLearnGT"
        " 2> {log} "
