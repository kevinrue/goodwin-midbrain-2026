# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"

localrules: concatenate_reference_fasta, fetch_genome_gtf

# target rules
# -----------------------------------------------------
rule all:
    input:
        "results/genome/genome.fa.gz",
        "results/genome/genome.gtf.gz",

# fetch genome sequence from Ensembl
# -----------------------------------------------------
# Download a list of genome FASTA files (see config/config.yaml).
# Decompress them and concatenate them into a single FASTA file.
def prepare_reference_genome_fasta_cmd(config):
    # constants
    tmp_dir = "tmp_genome"
    curl_template_cmd = "curl {source_url} > {tmp_dir}/{basename} && "
    # concatenate commands into a string
    cmd = f"mkdir -p {tmp_dir} && "
    for source_url in config["genome"]["fastas"]:
        basename = os.path.basename(source_url)
        cmd += curl_template_cmd.format(source_url=source_url, basename=basename, tmp_dir=tmp_dir)
    cmd += f"cat {tmp_dir}/*.fa.gz > results/genome/genome.fa.gz && "
    cmd += f"rm -rf {tmp_dir}"
    return cmd


prepare_reference_genome_fasta_cmd_str = prepare_reference_genome_fasta_cmd(config)

rule concatenate_reference_fasta:
    output:
        "results/genome/genome.fa.gz",
    log:
        "logs/concatenate_reference_fasta.log",
    threads: 1
    shell:
        "({prepare_reference_genome_fasta_cmd_str}) 2> {log}"

# fetch genome annotations from Ensembl
# -----------------------------------------------------
# Download a GTF file (see config/config.yaml).
# Decompress it and save it as a GTF file.
rule fetch_genome_gtf:
    output:
        "results/genome/genome.gtf.gz",
    params:
        source_url=config["genome"]["gtf"][0],
    log:
        "logs/fetch_genome_gtf.log",
    threads: 1
    shell:
        "(mkdir -p tmp_gtf &&"
        " curl {params.source_url} > tmp_gtf/genome.gtf.gz && "
        " zcat tmp_gtf/genome.gtf.gz | "
        "   grep -v '^#' | "
        "   grep -v '^Unmapped_Scaffold' | "
        "   grep -v '^rDNA' | "
        "   grep -v '^Y_mapped_Scaffold' | "
        "   grep -v '^211' | "
        "   gzip > {output} &&"
        " rm -rf tmp_gtf) 2> {log}"
